# --EnemyUnitPopup-- 9/4/2022 7:15:58 PM

$additional_Distance:1
$refresh_Info:60

:Once
	true ->displayEnabled
	null ->currentData

	SetMSGButton(0 true "Disable Enemy Unit Display" V4(1 0 0 1) "ConfigureDisplay" "hi")

	RegisterForMsg("ConfigureDisplay" dup)

	RegisterForMsg("RequestUnitInfo" dup)
	RegisterForMsg("ReceiveUnitInfo" dup)

	RegisterForMsg("MSG_FrameAdvance" "FrameAdvance")

:FrameAdvance
	if (<-displayEnabled !) return endif

	GetPointerTerrainCoords V2 ->pointerCoords
	if (GetGameFrameCount <-refresh_Info % eq0 <-pointerCoords <-oldCoords != ||)
		<-pointerCoords ->oldCoords
		
		false ->foundUnit
		GetListCount(<-*enemyUnitPositions) 0 do
			<-*enemyUnitPositions[i] ->currentPos
			
			<-currentPos.x <-pointerCoords.x - Abs ->xDiff
			<-currentPos.z <-pointerCoords.y - Abs ->zDiff
			
			if (<-xDiff <-additional_Distance <= <-zDiff <-additional_Distance <= &&)
				SendMverseMsg("RequestUnitInfo" <-*enemyUnitPositions[i])
				true ->foundUnit
				break
			endif
		loop
		if (<-foundUnit !) @ClearMSGButtons endif
	endif

:RequestUnitInfo
	GetUnitsInRange(<-_DATA 3 true true false 0 1 0)[0] ->unit
	if (<-unit IsNull) return endif

	CreateTable ->info

	GetUnitOfficialName(<-unit) ->info{"name"}
	
	GetUnitEnabled(<-unit) ->info{"enabled"}
	GetUnitArmed(<-unit) ->info{"armed"}
	GetUnitSupplied(<-unit) ->info{"supplied"}
	
	GetUnitHealth(<-unit) ->info{"health"}
	GetUnitMaxHealth(<-unit) ->info{"maxHealth"}
	
	GetUnitAmmo(<-unit) ->info{"ammo"}
	GetUnitMaxAmmo(<-unit) ->info{"maxAmmo"}

	SendMverseMsg("ReceiveUnitInfo" <-info)

:ReceiveUnitInfo
	" Enabled: " <-_DATA{"enabled"} @Stringify concat
	" Armed: " <-_DATA{"armed"} @Stringify concat3
	" Supplied: " <-_DATA{"supplied"} @Stringify concat3
	->status

	"Health: " round(<-_DATA{"health"} 1) "/" <-_DATA{"maxHealth"} concat4 ->health
	"Ammo: " round(<-_DATA{"ammo"} 0) "/" <-_DATA{"maxAmmo"} concat4 ->ammo

	SetMSGButton(1 true <-_DATA{"name"} V4(1 1 1 1) null null)
	SetMSGButton(2 true <-status V4(0 0 1 1) null null)
	SetMSGButton(3 true <-health V4(0 1 0 1) null null)
	SetMSGButton(4 true <-ammo V4(1 0 0 1) null null)

:ConfigureDisplay
	<-displayEnabled ! ->displayEnabled
	
	if (<-displayEnabled)
		SetMSGButton(0 true "Disable Enemy Unit Display" V4(1 0 0 1) "ConfigureDisplay" "hi")
	else
		SetMSGButton(0 true "Enable Enemy Unit Display" V4(0 1 0 1) "ConfigureDisplay" "hi")
	endif

:ClearMSGButtons
	5 1 do
		SetMSGButton(i false "" Vector0 null null)
	loop

:Stringify
	->value
	if (<-value eq0)
		"No"
	else
		"Yes"
	endif
